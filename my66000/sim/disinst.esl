// Before importing this, alias <somefd> as fdx.

    proc PReg(reg: _uint)
    {
	fdx.str(regname[reg]);
    }

    proc PAddr(addr: _uint64)
    {
	if _defined(GetSym) then
	{   var s: @[]_byte;
	    s = GetSym(addr);
	    if s != 0 then
	    {	fdx.str(s);
		return;
	    }
	}
	fdx.str("0x");
	fdx.hex64(_uint64(addr));
    }

    proc PWO(wo: Word)
    {
	fdx.chr('#');
	fdx.uint8(wo&0x3F);	// offset
	if ((wo>>6)&0x3F) != 0 then
	{   fdx.str(",#");
	    fdx.uint8((wo>>6)&0x3F);
	}
    }

    proc PFmt(inst: Inst, ip: Word, fmt: @[]_byte)
    {   var i: _uint;
	var c: _byte;
	var op: OP3 = OP3(inst);	// gets all the registers
	var t8: _int8;
	var t16: _int16;
	const Cnd3: [32][2]_byte =
	{   '??', '??', '??', '??', '??', '??', '??',
	    '??', '??', '??', '??', '??', '??',
	    '!=', '> ', '< ',
	    '??', '??', '??', '??', '??', '??', '??',
	    '??', '??', '??', '??', '??', '??',
	    '==', '>=', '<=',
	};
	i = 0;
	loop
	{   c = fmt[i];
	  exit c == 0;
	    if c
	    is 'C' then
		fdx.str(CBitNames[op.rd]);
	    is 'G' then		// signed I5 in Rs2
	    {   t8 = op.rs2;
		if t8 > 15 then t8 |= ~0xF;
		fdx.int8(t8);
	    }
	    is 'H' then		// signed I5 in Rs3
	    {   t8 = op.rs3;
		if t8 > 15 then t8 |= ~0xF;
		fdx.int8(t8);
	    }
	    is 'I' then		// I5 in Rs1
		fdx.uint8(op.rs1);
	    is 'J' then		// I5 in Rs2
		fdx.uint8(op.rs2);
	    is 'K' then		// I5 in Rs3
		fdx.uint8(op.rs3);
	    is 'L' then		// signed I10 in Rs3, Rs2
	    {   t16 = (op.rs3<<5)|op.rs2;
		if t16 > 511 then t16 &= ~0x1FF;
		fdx.int16(t16);
	    }
	    is 'M' then		// optional width for dynamic shifts
	    {	if (inst & 0xFC0) != 0 then
		{   fdx.str(",#");
		    fdx.uint8((inst >> 6) & 0x3F);
		}
	    }
	    is 'N' then		// #w,#o or #o for status shifts
		PWO(inst);
	    is 'O' then		// #w,#o for insert
		PWO(Word(Read(ip+4, WORD)));
	    is 'P' then		// signed I16 in dec
		fdx.int16(_int16(_uint16(inst)));
	    is 'Q' then		// CND in loop
	    {	fdx.chr(Cnd3[op.rd][0]);
		if Cnd3[op.rd][1] != ' ' then fdx.chr(Cnd3[op.rd][1]);
	    }
	    is 'R' then		// register Rd
		PReg(op.rd);
	    is 'S' then		// register Rs1
		PReg(op.rs1);
	    is 'T' then		// register Rs2
		PReg(op.rs2);
	    is 'U' then		// register Rs3
		PReg(op.rs3);
	    is 'V' then
		;
	    is 'W' then		// I32 but second extension
	    {	fdx.str("0x");
		fdx.hex32(_uint32(Read(ip+8, WORD)));
	    }
	    is 'X' then		// signed I16 in hex
	    {	fdx.str("0x");
		fdx.hex16(inst&0xFFFF);
	    }
	    is 'Y' then		// signed I32
	    {	fdx.str("0x");
		fdx.hex32(_uint32(Read(ip+4, WORD)));
	    }
	    is 'Z' then		// I64
	    {	fdx.str("0x");
		fdx.hex64(Read(ip+4, QUAD));
	    }
	    else
		fdx.chr(c);
	    i += 1;
	}
    }

    proc disOpMem(inst: Inst, ip: Word)
    {   var op: OPMEM = OPMEM(inst);
	var addr: _uint64;
	var minor: 0..0b111111;
	var disp: _uint;
	const OpName: [64]@[]_byte =
	{ "ldub","lduh","lduw","ldd" ,"ldsb","ldsh","ldsw","la",   // 0b000xxx
	  "stb", "sth", "stw", "std", "???", "pre", "push","???",  // 0b001xxx
	  "???", "???", "???", "ldm", "stm", "???", "???", "???",  // 0b010xxx
	  "stb", "sth", "stw", "std", "stb", "sth", "stw", "std",  // 0b011xxx
	  "???", "???", "???", "calx","???", "???", "???", "cala", // 0b100xxx
	  "???", "???", "???", "???", "???", "???", "???", "???",  // 0b101xxx
	  "???", "???", "???", "???", "???", "???", "mm",  "ms",   // 0b110xxx
	  "ms",  "???", "???", "???", "ms",  "???", "???", "???"   // 0b111xxx
	};

	minor = op.minor;
	fdx.str(OpName[minor]);
	    if op.L == 1 then
		fdx.chr('l');
	    fdx.chr('\t');
	    if minor
	    is 0b011000 .. 0b011111 then	// store immediate
	    {	// address displacement, if any, comes before immediate data
		disp = 0;
		if op.I != 0 then
		{	disp = 4;	// 4 byte displacement
		    if op.D != 0 then
			disp = 8;	// 8 byte displacement
		}
		fdx.str("#0x");
		if (minor & 0b100) == 0 then
		{   if minor == 0b011011 then	// STD
			fdx.hex64(Read(ip+disp+4, QUAD));
		    else
			fdx.hex32(_uint32(Read(ip+disp+4, WORD)));
		}
		else
		    fdx.int(_int(Sext(4, op.rd)));
		fdx.chr(',');
	    }
	    is 0b000000..0b010111 then	// normal register src/dst
	    {	PReg(op.rd);
		fdx.chr(',');
	    }
	    is 0b110110, 0b110111, 0b111000, 0b111100 then	// MM, MS
	    {	var cnt: Word;
		var val: _uint8;
		var offs: _uint = ip+4;
		PReg(op.ri); fdx.chr(',');
		if op.minor == 0b111000 then
		{   fdx.chr('#');
		    val = _uint8(Read(offs, WORD));
		    offs += 4;
		    fdx.uint8(val);
		}
		elif op.minor == 0b111100 then
		{   fdx.chr('#');
		    fdx.uint8(op.rd);
		}
		else
		    PReg(op.rd);
		fdx.chr(',');
		if op.I != 0 then
		{   fdx.chr('#');
		    if op.D == 0 then	// 32-bit
		    {   cnt = _int32(Read(offs, WORD));
			fdx.int32(_int32(cnt));
		    }
		    else
		    {   cnt = Read(offs, QUAD);
			fdx.str("0x");
			fdx.hex64(cnt);
		    }
		}
		else
		    PReg(op.rb);
		return;
	    }
	    if minor
	    is 0b010011, 0b010100 then	// LDM,STM
	    {	PReg(op.ri); fdx.chr(',');
		op.ri = 0;	// defeat indexing
	    }
	    fdx.chr('[');
	    if op.rb == 0 then PReg(32); else PReg(op.rb);
	    if op.ri != 0 then
	    {	fdx.chr(',');
		PReg(op.ri);
		if op.sc != 0 then
		{   fdx.str("<<");
		    fdx.uint(op.sc);
		}
	    }
	    if op.I != 0 then
	    {	fdx.chr(',');
		if op.D == 0 then	// 32-bit
		{   addr = _int32(Read(ip+4, WORD));
		    fdx.int32(_int32(addr));
		}
		else
		{   addr = Read(ip+4, QUAD);
		    fdx.str("0x");
		    fdx.hex64(addr);
		}
	    }
	    fdx.chr(']');
//	    if op.rb == 0 then
//	    {   fdx.str("\t; ");
//		PAddr(ip+addr);
//	    }
    }

    proc disOp2(inst: Inst, ip: Word)
    {   var op: OP2 = OP2(inst);
	var minor: 0..0b111111;
	const OpNameU: [64]@[]_byte =
	{ "???",  "cvtus","cvtsd","cvtud","???",  "???",  "???",  "???",
	  "???",  "???",  "???",  "???",  "???",  "???",  "???",  "???",
	  "???",  "cvtdu","cvtfu","cvtdf","rnd",  "???",  "???",  "???",
	  "???",  "???",  "???",  "???",  "???",  "???",  "???",  "???",
	  "???",  "add",  "mul",  "div",  "cmp",  "???",  "max",  "min",
	  "or",   "xor",  "and",  "???",  "???",  "???",  "???",  "???",
	  "???",  "fadd", "fmul", "fdiv", "fcmp", "???",  "fmax", "fmin",
	  "???",  "csgn", "eadd", "???",  "???",  "pow",  "atan2","???"
	};
	const OpNameS: [64]@[]_byte =
	{ "???",  "cvtsu","cvtsf","cvtuf","???",  "???",  "???",  "???",
	  "???",  "???",  "???",  "???",  "???",  "???",  "???",  "???",
	  "???",  "cvtds","cvtfs","cvtfd","rndf", "???",  "???",  "???",
	  "???",  "???",  "???",  "???",  "???",  "???",  "???",  "???",
	  "???",  "adds", "muls", "divs", "cmp",  "???",  "maxs", "mins",
	  "???",  "???",  "???",  "???",  "???",  "???",  "???",  "???",
	  "???",  "faddf","fmulf","fdivf","fcmpf","???",  "fmaxf","fminf",
	  "???",  "csgnf","eaddf","???",  "???",  "powf", "atan2f","???"
	};
	const OpFmt: [16]@[]_byte =	// I.D.s1.s2
	{   "R,S,T",	"R,S,-T",	"R,-S,T",	"R,-S,-T",
	    "R,S,#J",	"R,#I,T",	"R,S,#-J",	"R,#-I,T",
	    "R,S,#Y",	"R,#Y,T",	"R,-S,#Y",	"R,#Y,-T",
	    "R,S,#Z",	"R,#Z,T",	"R,-S,#Z",	"R,#Z,-T"
	};
	// Map I.s1.s2.S.D into I.D.s1.s2
	const MapSpec: [32]_byte =
	{   0x0, 0x4, 0x0, 0x4, 0x1, 0x5, 0x1, 0x5,
	    0x2, 0x6, 0x2, 0x6, 0x3, 0x7, 0x3, 0x7,
	    0x8, 0xC, 0x8, 0xC, 0x9, 0xD, 0x9, 0xD,
	    0xA, 0xE, 0xA, 0xE, 0xB, 0xF, 0xB, 0xF
	};
	minor = op.minor;
	{   if op.S != 0 then fdx.str(OpNameS[op.minor]);
			 else fdx.str(OpNameU[op.minor]);
	    fdx.chr('\t');
	    PFmt(inst, ip, OpFmt[MapSpec[(inst >> 11)&0b11111]]);
//fdx.chr('\t'); fdx.hex8(MapSpec[(inst >> 11)&0b11111]);
	}
    }

    proc disLoop(inst: Inst, ip: Word)
    {   var op: OPLOOP = OPLOOP(inst);
	var type: 0..3 = op.kind;

	const Fmt1u: [8]@[]_byte =
	{   "eq", "ne", "hs", "lo", "hi", "ls", "a", "n"
	};
	const Fmt1s: [8]@[]_byte =
	{   "eq", "ne", "ge", "lt", "gt", "le", "a", "n"
	};
	const Fmt2: [8]@[]_byte =
	{   "S,T,U",  "S,#J,U", "S,T,#K",  "S,#J,#K",
	    "S,#Y,U", "S,T,#Y", "S,#Y,#W", "S,#Z,#1"
	};

	fdx.str("loop"); fdx.uint(op.kind); fdx.chr('\t');
	if op.s == 0 then
	    PFmt(inst, ip, Fmt1u[op.cnd]);
	else
	    PFmt(inst, ip, Fmt1s[op.cnd]);
	fdx.chr(',');
	var iss: _uint = (inst >> 13) & 0b111;	// The I,s1,s2 bits
	PFmt(inst, ip, Fmt2[iss]);
    }

    proc disOp3(inst: Inst, ip: Word)
    {   var op: OP3 = OP3(inst);
	const MuxOpNames: [8]@[]_byte =
	{ "mux", "cmov","???", "???", "mux", "???", "mux", "???"
	};
	const OpFmt: [8]@[]_byte =
	{   "R,S,T,U",   "R,S,T,U",   "R,T",       "R,S",
	    "R,S,T,#Y",  "R,#Y",      "R,S,T,#Z",  "R,#Z"
	};
	const OpFmt4: [8]@[]_byte =
	{   "R,S,T,U",   "R,S,T,-U",  "R,S,-T,U",  "R,S,-T,-U",
	    "R,S,T,#Y",  "R,S,#Y,U",  "R,S,T,#Z",  "R,S,#Z,U"
	};

	if op.minor
	is 0b000 then	// FMAC
	{   fdx.str("fmac\t");
	    PFmt(inst, ip, OpFmt4[(inst>>13)&0b111]);
	}
	is 0b001 then		// the varieties of MUX
	{   fdx.str(MuxOpNames[(inst>>13)&0b111]); fdx.chr('\t');
	    PFmt(inst, ip, OpFmt[(inst>>13)&0b111]);
	}
	is 0b010 then	// INS
	{   fdx.str("ins\t");
	    // FIXME - do the S1 and S2 bits have any meaning
	    if op.I == 0 then
		PFmt(inst, ip, OpFmt[0]);
	    else
		PFmt(inst, ip, "R,S,T,#O");
	}
	is 0b100 then	// FMACF
	{   fdx.str("fmacf\t");
	    PFmt(inst, ip, OpFmt4[(inst>>13)&0b111]);
	}
	else
	    fdx.str("???");
    }

    proc disOp1(inst: Inst, ip: Word)
    {   var opa: OP1a = OP1a(inst);
	const OpName: [64]@[]_byte =
	{ "hr",  "???", "???", "???", "???", "???", "expon", "fract",
	  "pop", "ff1", "???", "???", "???", "???", "???", "???",
	  "cvt", "cvt", "???", "???", "rnd", "???", "???", "???",
	  "???", "???", "???", "???", "???", "???", "???", "???"
	};
	const OpFF1Name: [4]@[]_byte =
	{ "clz", "fl1", "ft1", "ctz" };
	const TransName: [32]@[]_byte =
	{ "fln2",   "fln",  "flog",      "???",
	  "fexp2",  "fexp", "fexp10",    "???",
	  "frcp",   "???",   "???",      "???",
	  "frsqrt", "fsqrt", "???",      "???",
	  "fln2p1", "flnp1", "flogp1",   "???",
	  "fexp2m1","fexpm1","fexp10m1", "???",
	  "fsin",   "fcos",  "ftan",     "???",
	  "fasin",  "facos", "fatan",    "???"
	};
	const HRFunc: [32]_byte =
	{ 'I', 'M', 'A', 'E', 'S', 'F', 'R', 'W',
	  '?', '?', '?', '?', '?', '?', '?', '?',
	  '?', '?', '?', '?', '?', '?', '?', '?',
	  '?', '?', '?', '?', '?', '?', '?', '?'
	};

	if opa.minor == 0b000000 then	// HR
	{   if opa.func == 0b00000 then 	// read/write IP
	    {	if opa.w != 0 then
		{   if opa.r != 0 then
		    {	fdx.str("calli\t");
			PReg(opa.rs1);
		    }
		    else
		    {	if opa.rs1 != 0 then
			{   fdx.str("jmp\t");
			    PReg(opa.rs1);
			}
			else
			    fdx.str("ret");
		    }
		}
	    }
	    else
	    {	fdx.str("hr");
		if opa.r != 0 then
		{   if opa.w != 0 then
			fdx.str("x\t");
		    else
			fdx.str("r\t");
		    PReg(opa.rd);
		}
		if opa.w != 0 then
		{   if opa.r == 0 then
			fdx.str("w\t");
		    else
			fdx.chr(',');
		    PReg(opa.rs1);
		}
		fdx.chr(',');
		fdx.chr(HRFunc[opa.func]);
	    }
	}
	else
	{   var opb: OP1b = OP1b(inst);
	    const OpFmt: [32]@[]_byte =	// I s1 sd S D
	    {   "R,S",	"R,#I",	"R,S",	"R,#I",
		"R,S",	"R,#I",	"R,S",	"R,#I",	// FIXME - sd ignored
		"R,-S",	"R,#-I","R,-S",	"R,#-I",
		"R,-S",	"R,#-I","R,-S",	"R,#-I",
		"R,#Y",	"R,#Z",	"R,#Y",	"R,#Z",
		"R,#Y",	"R,#Z",	"R,#Y",	"R,#Z",
		"R,#-Y","R,#-Z","R,#-Y","R,#-Z",
		"R,#-Y","R,#-Z","R,#-Y","R,#-Z"
	    };
	    if opb.minor
	    is 0b00101 then
	    {	if opb.func == 0 then
		{   if opb.s == 0 then fdx.str("abs");
				  else fdx.str("mov");
		}
		else	// assume opb.func = 1
		{   fdx.str("fabs");
		    if opb.s != 0 then fdx.chr('f');
		}
	    }
	    is 0b00110..0b00111	then	// EXPON,FRACT
	    {	fdx.str(OpName[opb.minor]);
		if opb.s != 0 then fdx.chr('f');
	    }
	    is 0b01001 then		// FF1 family
	    {	if opb.func <= 4 then fdx.str(OpFF1Name[opb.func]);
		else fdx.str("???");
	    }
	    is 0b01000, 0b01010..0b01111 then	// POP
		fdx.str(OpName[opb.minor]);
	    is 0b11000 then		// TRANS
	    {   fdx.str(TransName[opb.func]);
		if opb.s != 0 then fdx.chr('f');
	    }
	    is 0b11101 then		// VEC
	    {	var reg: _uint;
		var mask: _uint32;
		mask = _uint32(Read(ip+4, WORD));
		fdx.str("vec"); fdx.chr('\t'); PReg(opb.rd);
		fdx.str(',{');
		if mask != 0 then
		{   loop
		    {   reg = _ctz(mask);
			PReg(reg);
			mask &= ~(1 << reg);
		      exit mask == 0;
			fdx.chr(',');
		    }
		}
		fdx.chr('}');
		return;
	    }
	    fdx.chr('\t');
	    if opb.sd != 0 then fdx.chr('-');
	    PFmt(inst, ip, OpFmt[(inst >> 11)&0b11111]);
	}
    }

    proc PShadow(e: _uint, t: _uint)
    {
	while t > 0 do
	{   fdx.chr('T');
	    t -= 1;
	}
	while e > 0 do
	{   fdx.chr('F');
	    e -= 1;
	}
    }

    proc disShfR(inst: Inst, ip: Word)
    {	var op: OPSHFR = OPSHFR(inst);
	const ShfROpNames: [16]@[]_byte =
	{   "???", "???", "???", "???",
	    "???", "???", "rol", "ror",
	    "srl", "sra", "sll", "sla",
	    "bitr","???", "???", "???"
	};

	fdx.str(ShfROpNames[op.minor]); fdx.chr('\t');
	PFmt(inst, ip, "R,S,TM");
    }

    proc disPredShf(inst: Inst, ip: Word)
    {	var op: OPSHF = OPSHF(inst);
	const ShfOpNames: [16]@[]_byte =
	{   "pb1", "pcnd","???", "???",
	    "???", "???", "rol", "ror",
	    "srl", "sra", "sll", "sla",
	    "bitr","???", "???", "???"
	};

	fdx.str(ShfOpNames[op.minor]); fdx.chr('\t');
	if op.minor
	is 0b0000 then
	{   var opb: OPPB = OPPB(inst);
	    fdx.uint8(opb.bit); fdx.chr(',');
	    PReg(opb.rs1); fdx.chr(',');
	    PShadow(opb.else, opb.then);
	}
	is 0b0001 then
	{   var opc: OPPC = OPPC(inst);
	    fdx.str(CondNames[opc.cond]); fdx.chr(',');
	    PReg(opc.rs1); fdx.chr(',');
	    PShadow(opc.else, opc.then);
	}
	else
	    PFmt(inst, ip, "R,S,N");
    }

    proc disOpBb(inst: Inst, ip: Word)
    {	var op: OPBB = OPBB(inst);
	var adr: Word;

	fdx.str("b");
	fdx.str(CBitNames[op.bit]); fdx.chr('\t');
	PReg(op.rs1); fdx.chr(',');
	adr = Word(Sext(15, inst&0xFFFF)) << 2;
	adr += ip;
	fdx.str("0x"); fdx.hex32(_uint32(adr));
    }

    proc disOpBc(inst: Inst, ip: Word)
    {	var op: OPBC = OPBC(inst);
	var adr: Word;

	if Conditions(op.cond)
	is RET then fdx.str("ret");
	is SVR then fdx.str("svr");
	is SVC then fdx.str("svc");
	else
	{   fdx.str("b");
	    fdx.str(CondNames[op.cond]); fdx.chr('\t');
	    PReg(op.rs1); fdx.chr(',');
	    adr = Word(Sext(15, inst&0xFFFF)) << 2;
	    adr += ip;
	    fdx.str("0x"); fdx.hex32(_uint32(adr));
	}
    }

    proc disOpJt(inst: Inst, ip: Word)
    {	var op: OPTT = OPTT(inst);
	var adr: Word;
	const size: [4]_byte = { 'b', 'h', 'w', 'd' };

	fdx.str("jtt"); fdx.chr(size[op.sz]); fdx.chr('\t');
	PReg(op.rs1); fdx.str(",#");
	fdx.uint16(op.imm);
    }

    proc disOpBr(inst: Inst, ip: Word)
    {	var op: OPBR = OPBR(inst);
	var adr: Word;

	if (op.major & 1) != 0 then fdx.str("call\t");
	else fdx.str("br\t");

	adr = ip + Word(Sext(25, inst&0x03FF_FFFF) << 2);
	fdx.str("0x"); fdx.hex32(_uint32(adr));
	fdx.str("\t; ");
	PAddr(adr);
    }

    proc disOpMemImm(inst: Inst, ip: Word)
    {   var op: OPMEMIMM = OPMEMIMM(inst);
	const OpName: [16]@[]_byte =
	{ "ldub","lduh","lduw","ldd" ,"ldsb","ldsh","ldsw","exit",
	  "stb", "sth", "stw", "std", "enter","ldm", "stm", "???"
	};

	fdx.str(OpName[op.major&0b1111]); fdx.chr('\t');
	PReg(op.rd); fdx.chr(',');
	if op.major&0b1111
	is 0b0111, 0b1100 then	// exit, enter
	{   PReg(op.rb); fdx.chr(',');
	    fdx.uint16(op.imm & 7); fdx.chr(',');
	    fdx.uint16(op.imm & 0xFFF8);
	}
	else
	{   fdx.chr('[');
	    if op.rb == 0 then PReg(32); else PReg(op.rb); fdx.chr(',');
	    fdx.int16(_int16(op.imm));
	    fdx.chr(']');
	}
    }


    proc disOpImm(inst: Inst, ip: Word)
    {	var op: OPIMM = OPIMM(inst);
	const OpName: [16]@[]_byte =
	{ "???", "add", "mul", "div", "cmp", "???", "???", "???",
	  "or",  "xor", "and", "mov", "???", "???", "???", "???"
	};
	const OpFmt = "R,S,#P\t; X";
	const OpMov = "R,#P\t; X";

	fdx.str(OpName[op.major&0b1111]); fdx.chr('\t');
	if op.major&0b1111 != 0b1011 then	// not MOV
	    PFmt(inst, ip, OpFmt);
	else
	    PFmt(inst, ip, OpMov);
    }

    proc disCarry(inst: Inst, ip: Word)
    {	var op: OPCARRY = OPCARRY(inst);
	var bits: _uint;

	fdx.str("carry\t");
	PReg(op.rd); fdx.str(",{");
	bits = op.imm;
	while (bits != 0) do
	{ if bits & 3
	  is 0 then fdx.chr('-');
	  is 1 then fdx.chr('I');
	  is 2 then fdx.chr('O');
	  is 3 then fdx.str("IO");
	  bits = bits >> 2;
	  if bits != 0 then fdx.chr(',');
	}
	fdx.chr('}');
    }

    proc disVec(inst: Inst, ip: Word)
    {	var op: OPVEC = OPVEC(inst);
	var bits, tmp: _uint;
	var reg: _uint;

	fdx.str("vec\t");
	tmp = op.imm;
	bits = ((tmp & 0x1F8000) << 10) |
	       ((tmp & 0x007FFF) << 1);
	PReg(op.rd); fdx.str(",{");
	if bits != 0 then
	{   loop
	    {   reg = _ctz(bits);
		PReg(reg);
		bits &= ~(1 << reg);
	      exit bits == 0;
		fdx.chr(',');
	    }
	}
	fdx.chr('}');
    }


    proc InstLen(inst: Inst): _uint
    {   var len, major: _uint;

	len = 1;
	if (inst & 0x8000) != 0 then	// I (or D) bit set
	{
	    major = inst >> 26;
//fdx.str("InstLen "); fdx.hex8(major);
//fdx.str(" d="); fdx.uint((inst >> 11)&1);
//fdx.str(" s1="); fdx.uint((inst >> 14)&1);
//fdx.nl();
	    if major <= 0b010101 then
	    {	if major
		is 0b001001, 0b001010, 0b001101 then // XOP1 LD/ST, XOP2, XOP5
		{   len += 1;			// I-bit
		    len += (inst >> 11)&1;	// d-bit
		}
		is 0b001100, 0b010101 then		// XOP4, LOOP
		{   len += 1;			// I-bit
		    len += (inst >> 14)&1;	// s1-bit
		}
	    }
	}
	if (inst & 0x1C00_0780) == 0x0400_0300 then // stX imm long
	{   len += 1;
	    if ((inst >> 5) & 3) == 3 then	// std imm
		len += 1;
	}
	if (inst & 0xFC00_07E0) == 0x2400_0700 then // MS with follwing byte
	    len += 1;
	return len * 4;	// words to bytes
    }

    proc disInst(ip: Addr): _uint
    {	var n, i: _uint;
	var inst: Inst;
	var major: 0..0b111111;

	inst = Read(ip, WORD);
	major = (inst >> 26) & 0b111111;
	n = InstLen(inst);
	fdx.chr(' ');
	fdx.hex32(Read(ip, WORD));
	fdx.chr('\t');
	if major
	is 0b000000,
	   0b001111,
	   0b101111, 0b110000 then	// unallocated
	{
	    fdx.str("Unallocated instruction ");
	    fdx.hex32(inst);
	}
	is 0b000110, 0b000111 then disPredShf(inst, ip);
	is 0b001000	      then disShfR(inst, ip);
	is 0b001001	      then disOpMem(inst, ip);
	is 0b001010           then disOp2(inst, ip);
	is 0b001100	      then disOp3(inst, ip);
	is 0b001101	      then disOp1(inst, ip);
	is 0b010101	      then disLoop(inst, ip);
	is 0b011000..0b011001 then disOpBb(inst, ip);
	is 0b011010	      then disOpBc(inst, ip);
	is 0b011011           then disOpJt(inst, ip);
	is 0b011110..0b011111 then disOpBr(inst, ip);
	is 0b100000..0b101110 then disOpMemImm(inst, ip);
	is 0b110001..0b110100,
	   0b111000..0b111011 then disOpImm(inst, ip);
	is 0b111100	      then disCarry(inst, ip);
	is 0b111101           then disVec(inst, ip);
	is 0b111111 then	// Syscall hack
	{   if inst == 0xFFAA5501 then
		fdx.str("<syscall>");
	    elif (inst & 0xFFFFFF00) == 0xFFAA0000 then
		fdx.str("<breakpoint>");
	    else
	    {
		fdx.str("Unimplemented instruction ");
	    fdx.hex32(inst);
	    }
	}
	else
	{
	    fdx.str("Unimplemented instruction ");
	    fdx.hex32(inst);
	}
	i = 4;
	while i < n do
	{   fdx.str("\n          ");
	    fdx.hex32(Read(ip+i, WORD));
	    i += 4;
	}
	return n;
    }
